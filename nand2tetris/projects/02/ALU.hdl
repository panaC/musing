// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the output bits:
 * zr = (out == 0, 1, 0)
 * ng = (out < 0,  1, 0)
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // (out == 0, 1, 0)
        ng;      // (out < 0,  1, 0)

    PARTS:
    
    // zx
    Mux16(a=x,b[0..15]=false,sel=zx,out=xWithZx);
    // nx
    Not16(in=xWithZx,out=NotX);
    Mux16(a=xWithZx,b=NotX,sel=nx,out=xWithZxNx);
    
    // zy
    Mux16(a=y,b[0..15]=false,sel=zy,out=yWithZy);
    // ny
    Not16(in=yWithZy,out=NotY);
    Mux16(a=yWithZy,b=NotY,sel=ny,out=yWithZyNy);

    // out
    Add16(a=xWithZxNx,b=yWithZyNy,out=outAdd);
    And16(a=xWithZxNx,b=yWithZyNy,out=outAnd);
    Mux16(a=outAnd,b=outAdd,sel=f,out=outMuxAddAnd);
    Not16(in=outMuxAddAnd,out=notOutMuxAddAnd);
    Mux16(a=outMuxAddAnd,b=notOutMuxAddAnd,sel=no,out=oout,out[15]=ng,out=out);

    // zr
    Or16Way(in=oout,out=o7);
    Not(in=o7,out=zr);

}
